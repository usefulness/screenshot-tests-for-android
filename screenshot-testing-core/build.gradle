plugins {
  alias(libs.plugins.starter.library.android)
  alias(libs.plugins.kotlin.serialization)
  alias(libs.plugins.kotlinx.binarycompatibility)
  alias(libs.plugins.drewhamilton.poko)
  id("com.starter.publishing")
}

poko {
  pokoAnnotation.set "io/github/usefulness/testing/screenshot/internal/Poko"
}

android {
  namespace = "io.github.usefulness.testing.screenshot.tests"
  defaultConfig {
    minSdk = 21
    testInstrumentationRunner "io.github.usefulness.testing.screenshot.CustomScreenshotTestRunner"
  }
  testBuildType = "release"
  testOptions {
    targetSdk = 36
  }
  publishing {
    singleVariant("release")
  }
  lint {
    disable += "UnknownNullness"
  }
}

androidComponents {
  beforeVariants(selector().withBuildType("release")) { builder ->
    builder.enable = true
  }
  beforeVariants(selector().withBuildType("debug")) { builder ->
    builder.enable = false
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  testLogging {
    events "skipped", "failed"
  }
}

dependencies {
  implementation(libs.androidx.annotation)
  implementation(libs.androidx.core)
  implementation(libs.androidtest.runner)
  implementation(libs.kotlinx.serialization.json.core)

  testImplementation(libs.junit.jupiter)
  testImplementation(libs.assertj.core)

  androidTestImplementation(libs.assertj.core)
  androidTestImplementation(libs.espresso.core)
  androidTestImplementation(libs.androidtest.core)
  androidTestImplementation(libs.androidtest.rules)
  androidTestImplementation(libs.androidtest.junitext)
}
