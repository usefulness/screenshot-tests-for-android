import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id("java-gradle-plugin")
  alias(libs.plugins.starter.library.kotlin)
  id("com.starter.publishing")
  alias(libs.plugins.gradle.pluginpublish)
}

final GENERATED_PACKAGE_NAME = "io.github.usefulness.testing.screenshot.generated"
final GENERATED_BUILD_DIR = layout.buildDirectory.map { it.dir("generated/config/${GENERATED_PACKAGE_NAME.replace('.', '/')}")}

tasks.register("generateBuildConfig") {
  final version = project.version
  doLast {
    def generatedDir = GENERATED_BUILD_DIR.get().asFile
    generatedDir.deleteDir()
    generatedDir.mkdirs()

    final className = "ScreenshotTestBuildConfig"
    final configClass = new File(generatedDir, "${className}.kt")
    BufferedWriter writer = configClass.newWriter()
    try {
      writer.writeLine("package $GENERATED_PACKAGE_NAME")
      writer.writeLine("")
      writer.writeLine("""
        object $className { 
            const val PLUGIN_VERSION = \"${version.toString()}\" 
            const val ANDROID_TEST_SERVICES_VERSION = \"${libs.androidx.test.services.get().version}\" 
        }
      """.stripIndent())
      writer.flush()
    } finally {
      writer.close()
    }
  }
}

tasks.withType(KotlinCompile).configureEach {
  compilerOptions {
    apiVersion = KotlinVersion.KOTLIN_1_8
    languageVersion = KotlinVersion.KOTLIN_1_8
  }
}

tasks.named("compileKotlin") {
  dependsOn("generateBuildConfig")
  sourceSets {
    main {
      kotlin.srcDir(GENERATED_BUILD_DIR)
      resources.srcDirs 'src/py'
      resources {
        exclude '**/*.pyc'
        exclude '**/test_*.py'
        exclude '**/fixtures/**'
      }
    }
  }
}

tasks.named('clean') {
  delete GENERATED_BUILD_DIR
}

gradlePlugin {
  website = "https://github.com/usefulness/screenshot-tests-for-android/"
  vcsUrl = "https://github.com/usefulness/screenshot-tests-for-android.git"
  plugins {
    screenshotTesting {
      id = 'io.github.usefulness.screenshot-testing-plugin'
      displayName = 'Screenshot Tests for Android'
      description = "Generate fast deterministic screenshots during Android instrumentation tests. Configuration Cache compatibility included"
      implementationClass = 'com.facebook.testing.screenshot.build.ScreenshotsPlugin'
      tags.set(["android", "screenshots", "espresso", "screenshot-testing"])
    }
  }
}

dependencies {
  compileOnly(libs.agp.gradle.api)

  testImplementation(libs.agp.gradle.api)
  testImplementation(gradleTestKit())
  testImplementation(libs.junit.jupiter)
  testImplementation(libs.assertj.core)
}
